load("//tools/build:rules.bzl", "apic", "mm_library", "cc_copts", "cc_defines")

apic(
    name = "gles_cc",
    api = "//gapis/api/gles:api",
    templates = [
        "//gapis/api/templates:specific_gfx_api.cpp",
    ]
)

apic(
    name = "gles_h",
    api = "//gapis/api/gles:api",
    templates = [
        "//gapis/api/templates:specific_gfx_api.h",
    ]
)

apic(
    name = "vulkan_cc",
    api = "//gapis/api/vulkan:api",
    templates = [
        "//gapis/api/templates:specific_gfx_api.cpp",
        "//gapis/api/templates:vulkan_gfx_api_extras.cpp",
    ]
)

apic(
    name = "vulkan_h",
    api = "//gapis/api/vulkan:api",
    templates = [
        "//gapis/api/templates:specific_gfx_api.h",
    ]
)

filegroup(
    name = "headers",
    srcs = glob(["*.h", "*.inc"]) + [":gles_h", ":vulkan_h"],
    visibility = ["//visibility:public"],
)

mm_library(
    name = "darwin_renderer",
    srcs = glob(["osx/*.mm"]),
    hdrs = [":headers"],
    deps = [
        "//core/cc",
    ],
    defines = cc_defines(),
)

cc_library(
    name = "gapir",
    srcs = glob(["*.cpp"], exclude = ["*_test.cpp"]) + select({
        "//tools/build:linux": glob(["linux/*.cpp"]),
        "//tools/build:darwin": glob(["osx/*.cpp"]),
        "//tools/build:windows": glob(["windows/*.cpp"]),
        # Android
        "//conditions:default": glob(["android/*.cpp"]),
    }) + [":gles_cc", ":gles_h", ":vulkan_cc", ":vulkan_h"],
    defines = cc_defines(),
    hdrs = [":headers"],
    deps = [
         "//core/cc",
    ] + select({
        "//tools/build:darwin": [":darwin_renderer"],
        "//conditions:default": [],
    }),
    copts = cc_copts(),
    linkopts = select({
        "//tools/build:linux": ["-ldl", "-lGL", "-lX11"],
        "//tools/build:darwin": ["-framework Cocoa", "-framework OpenGL"],
        "//tools/build:windows": ["-lopengl32", "-lgdi32"],
        # Android
        "//conditions:default": ["-lm", "-llog", "-lEGL", "-lGLESv2", "-landroid"],
    }),
    visibility = ["//visibility:public"],
)
