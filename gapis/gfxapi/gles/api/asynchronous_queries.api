// Copyright (C) 2017 Google Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

@internal
class Query {
  // Table 21.31: Query Object State
  // GLuint QueryResult = 0 // TODO: or GL_FALSE?
  // GLboolean QueryResultAvailable = GL_FALSE
  @unused string Label
}

@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glBeginQuery.xhtml", GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glBeginQuery.xhtml", GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glBeginQuery.xhtml", GLES32)
cmd void glBeginQuery(GLenum target, QueryId query) {
  minRequiredVersion(3, 0)
  switch (target) {
    case GL_ANY_SAMPLES_PASSED, GL_ANY_SAMPLES_PASSED_CONSERVATIVE,
        GL_TRANSFORM_FEEDBACK_PRIMITIVES_WRITTEN: {
      // version 3.0
    }
    case GL_PRIMITIVES_GENERATED: {
      minRequiredVersion(3, 2)
    }
    default: {
      glErrorInvalidEnum(target)
    }
  }
  ctx := GetContext()
  ctx.ActiveQueries[target] = query
}

@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glDeleteQueries.xhtml", GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glDeleteQueries.xhtml", GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glDeleteQueries.xhtml", GLES32)
cmd void glDeleteQueries(GLsizei count, const QueryId* queries) {
  minRequiredVersion(3, 0)

  q := queries[0:count]
  ctx := GetContext()
  for i in (0 .. count) {
    id := q[i]
    if id != 0 {
      delete(ctx.Objects.Queries, id)
    }
  }
}

@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glBeginQuery.xhtml", GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glBeginQuery.xhtml", GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glBeginQuery.xhtml", GLES32)
cmd void glEndQuery(GLenum target) {
  minRequiredVersion(3, 0)
  switch (target) {
    case GL_ANY_SAMPLES_PASSED, GL_ANY_SAMPLES_PASSED_CONSERVATIVE,
        GL_TRANSFORM_FEEDBACK_PRIMITIVES_WRITTEN: {
      // version 3.0
    }
    case GL_PRIMITIVES_GENERATED: {
      minRequiredVersion(3, 2)
    }
    default: {
      glErrorInvalidEnum(target)
    }
  }
  ctx := GetContext()
  delete(ctx.ActiveQueries, target)
}

@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGenQueries.xhtml", GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGenQueries.xhtml", GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGenQueries.xhtml", GLES32)
cmd void glGenQueries(GLsizei count, QueryId* queries) {
  minRequiredVersion(3, 0)

  q := queries[0:count]
  ctx := GetContext()
  for i in (0 .. count) {
    id := as!QueryId(?)
    // TODO: This should only reserve the name, but not create the object.
    ctx.Objects.Queries[id] = new!Query()
    q[i] = id
  }
}

@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGetQueryObjectuiv.xhtml", GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGetQueryObjectuiv.xhtml", GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGetQueryObjectuiv.xhtml", GLES32)
cmd void glGetQueryObjectuiv(QueryId query, GLenum parameter, GLuint* value) {
  minRequiredVersion(3, 0)
  switch (parameter) {
    case GL_QUERY_RESULT, GL_QUERY_RESULT_AVAILABLE: {
      // version 3.0
    }
    default: {
      glErrorInvalidEnum(parameter)
    }
  }

  value[0] = ?
}

@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGetQueryiv.xhtml", GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGetQueryiv.xhtml", GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGetQueryiv.xhtml", GLES32)
cmd void glGetQueryiv(GLenum target, GLenum parameter, GLint* value) {
  minRequiredVersion(3, 0)
  switch (target) {
    case GL_ANY_SAMPLES_PASSED, GL_ANY_SAMPLES_PASSED_CONSERVATIVE,
        GL_TRANSFORM_FEEDBACK_PRIMITIVES_WRITTEN: {
      // version 3.0
    }
    case GL_PRIMITIVES_GENERATED: {
      minRequiredVersion(3, 2)
    }
    case GL_TIMESTAMP_EXT: {
      requiresExtension(GL_EXT_disjoint_timer_query)
    }
    default: {
      glErrorInvalidEnum(target)
    }
  }
  switch (parameter) {
    case GL_CURRENT_QUERY: {
      // version 3.0
      ctx := GetContext()
      if (target in ctx.ActiveQueries) {
        value[0] = as!GLint(ctx.ActiveQueries[target])
      } else {
        value[0] = 0
      }
    }
    case GL_QUERY_COUNTER_BITS_EXT: {
      requiresExtension(GL_EXT_disjoint_timer_query)
      value[0] = ?
    }
    default: {
      glErrorInvalidEnum(parameter)
    }
  }
}

@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glIsQuery.xhtml", GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glIsQuery.xhtml", GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glIsQuery.xhtml", GLES32)
cmd GLboolean glIsQuery(QueryId query) {
  minRequiredVersion(3, 0)

  ctx := GetContext()
  return as!GLboolean(query in ctx.Objects.Queries)
}
