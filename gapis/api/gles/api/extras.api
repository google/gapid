// Copyright (C) 2017 Google Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// StaticContextState is an atom extra used to describe a context's non-changing state.
@internal @serialize
class StaticContextState {
  Constants Constants
  string    ThreadName
}

@post_fence
extern ref!StaticContextState GetEGLStaticContextState(EGLDisplay display, EGLContext context)

// DynamicContextState is an atom extra used to describe a context's potentially changing state.
@internal @serialize
class DynamicContextState {
  GLsizei BackbufferWidth
  GLsizei BackbufferHeight
  GLenum  BackbufferColorFmt
  GLenum  BackbufferDepthFmt
  GLenum  BackbufferStencilFmt
  bool    ResetViewportScissor
  bool    PreserveBuffersOnSwap
  // TODO: Currently unused
  @unused GLuint RedSize
  @unused GLuint GreenSize
  @unused GLuint BlueSize
  @unused GLuint AlphaSize
  @unused GLuint DepthSize
  @unused GLuint StencilSize
}

@post_fence
extern ref!DynamicContextState GetEGLDynamicContextState(EGLDisplay display, EGLSurface surface, EGLContext context)

// ProgramInfo is an atom extra used to describe linked shader program.
@internal @serialize
class ProgramInfo {
  GLboolean                                   LinkStatus
  string                                      InfoLog
  map!(AttributeIndex, ActiveAttribute)       ActiveAttributes
  map!(UniformIndex, ActiveUniform)           ActiveUniforms
  map!(UniformBlockIndex, ActiveUniformBlock) ActiveUniformBlocks
}

@post_fence
extern ref!ProgramInfo GetProgramInfoExtra(ref!Context ctx, ProgramId programId)

sub void ApplyProgramInfoExtra(ProgramId programId, ref!ProgramInfo info) {
  if (info != null) {
    ctx := GetContext()
    program := ctx.Objects.Programs[programId]
    program.LinkStatus = info.LinkStatus
    program.InfoLog = info.InfoLog
    program.ActiveAttributes = null
    program.ActiveUniforms = null
    program.Uniforms = null
    for _, attributeIndex, aa in info.ActiveAttributes {
      aa.Type = hintAtAttributeType(aa.Type)
      program.ActiveAttributes[attributeIndex] = aa
    }
    for _, uniformIndex, au in info.ActiveUniforms {
      au.Type = hintAtUniformType(au.Type)
      program.ActiveUniforms[uniformIndex] = au
      for arrayIndex in (0 .. as!UniformLocation(au.ArraySize)) {
        // TODO: Locations do not have to be consecutive.
        program.Uniforms[au.Location + arrayIndex] = Uniform(Value: make!u8(0), Type: au.Type)
      }
      if au.BlockIndex == 0xFFFFFFFF {
        program.DefaultUniformBlock.ActiveUniforms[uniformIndex] = au // Copy for convenience
      }
    }
    for _, blockIndex, aub in info.ActiveUniformBlocks {
      tmp := aub
      for _, uniformIndex, au in info.ActiveUniforms {
        au.Type = hintAtUniformType(au.Type)
        if au.BlockIndex == blockIndex {
          tmp.ActiveUniforms[uniformIndex] = au // Copy for convenience
        }
      }
      program.ActiveUniformBlocks[blockIndex] = tmp
    }
  }
}

// hintAtUniformType simply return the single argument unaltered.
// It is used to add enum name hint information for static analysis.
sub GLenum hintAtUniformType(GLenum ty) {
  return switch ty {
    case GL_FLOAT,
        GL_FLOAT_VEC2,
        GL_FLOAT_VEC3,
        GL_FLOAT_VEC4,
        GL_DOUBLE,
        GL_DOUBLE_VEC2,
        GL_DOUBLE_VEC3,
        GL_DOUBLE_VEC4,
        GL_INT,
        GL_INT_VEC2,
        GL_INT_VEC3,
        GL_INT_VEC4,
        GL_UNSIGNED_INT,
        GL_UNSIGNED_INT_VEC2,
        GL_UNSIGNED_INT_VEC3,
        GL_UNSIGNED_INT_VEC4,
        GL_BOOL,
        GL_BOOL_VEC2,
        GL_BOOL_VEC3,
        GL_BOOL_VEC4,
        GL_FLOAT_MAT2,
        GL_FLOAT_MAT3,
        GL_FLOAT_MAT4,
        GL_FLOAT_MAT2x3,
        GL_FLOAT_MAT2x4,
        GL_FLOAT_MAT3x2,
        GL_FLOAT_MAT3x4,
        GL_FLOAT_MAT4x2,
        GL_FLOAT_MAT4x3,
        GL_DOUBLE_MAT2,
        GL_DOUBLE_MAT3,
        GL_DOUBLE_MAT4,
        GL_DOUBLE_MAT2x3,
        GL_DOUBLE_MAT2x4,
        GL_DOUBLE_MAT3x2,
        GL_DOUBLE_MAT3x4,
        GL_DOUBLE_MAT4x2,
        GL_DOUBLE_MAT4x3,
        GL_SAMPLER_1D,
        GL_SAMPLER_2D,
        GL_SAMPLER_3D,
        GL_SAMPLER_CUBE,
        GL_SAMPLER_1D_SHADOW,
        GL_SAMPLER_2D_SHADOW,
        GL_SAMPLER_1D_ARRAY,
        GL_SAMPLER_2D_ARRAY,
        GL_SAMPLER_1D_ARRAY_SHADOW,
        GL_SAMPLER_2D_ARRAY_SHADOW,
        GL_SAMPLER_2D_MULTISAMPLE,
        GL_SAMPLER_2D_MULTISAMPLE_ARRAY,
        GL_SAMPLER_CUBE_SHADOW,
        GL_SAMPLER_BUFFER,
        GL_SAMPLER_2D_RECT,
        GL_SAMPLER_2D_RECT_SHADOW,
        GL_INT_SAMPLER_1D,
        GL_INT_SAMPLER_2D,
        GL_INT_SAMPLER_3D,
        GL_INT_SAMPLER_CUBE,
        GL_INT_SAMPLER_1D_ARRAY,
        GL_INT_SAMPLER_2D_ARRAY,
        GL_INT_SAMPLER_2D_MULTISAMPLE,
        GL_INT_SAMPLER_2D_MULTISAMPLE_ARRAY,
        GL_INT_SAMPLER_BUFFER,
        GL_INT_SAMPLER_2D_RECT,
        GL_UNSIGNED_INT_SAMPLER_1D,
        GL_UNSIGNED_INT_SAMPLER_2D,
        GL_UNSIGNED_INT_SAMPLER_3D,
        GL_UNSIGNED_INT_SAMPLER_CUBE,
        GL_UNSIGNED_INT_SAMPLER_1D_ARRAY,
        GL_UNSIGNED_INT_SAMPLER_2D_ARRAY,
        GL_UNSIGNED_INT_SAMPLER_2D_MULTISAMPLE,
        GL_UNSIGNED_INT_SAMPLER_2D_MULTISAMPLE_ARRAY,
        GL_UNSIGNED_INT_SAMPLER_BUFFER,
        GL_UNSIGNED_INT_SAMPLER_2D_RECT,
        GL_IMAGE_1D,
        GL_IMAGE_2D,
        GL_IMAGE_3D,
        GL_IMAGE_2D_RECT,
        GL_IMAGE_CUBE,
        GL_IMAGE_BUFFER,
        GL_IMAGE_1D_ARRAY,
        GL_IMAGE_2D_ARRAY,
        GL_IMAGE_2D_MULTISAMPLE,
        GL_IMAGE_2D_MULTISAMPLE_ARRAY,
        GL_INT_IMAGE_1D,
        GL_INT_IMAGE_2D,
        GL_INT_IMAGE_3D,
        GL_INT_IMAGE_2D_RECT,
        GL_INT_IMAGE_CUBE,
        GL_INT_IMAGE_BUFFER,
        GL_INT_IMAGE_1D_ARRAY,
        GL_INT_IMAGE_2D_ARRAY,
        GL_INT_IMAGE_2D_MULTISAMPLE,
        GL_INT_IMAGE_2D_MULTISAMPLE_ARRAY,
        GL_UNSIGNED_INT_IMAGE_1D,
        GL_UNSIGNED_INT_IMAGE_2D,
        GL_UNSIGNED_INT_IMAGE_3D,
        GL_UNSIGNED_INT_IMAGE_2D_RECT,
        GL_UNSIGNED_INT_IMAGE_CUBE,
        GL_UNSIGNED_INT_IMAGE_BUFFER,
        GL_UNSIGNED_INT_IMAGE_1D_ARRAY,
        GL_UNSIGNED_INT_IMAGE_2D_ARRAY,
        GL_UNSIGNED_INT_IMAGE_2D_MULTISAMPLE,
        GL_UNSIGNED_INT_IMAGE_2D_MULTISAMPLE_ARRAY,
        GL_UNSIGNED_INT_ATOMIC_COUNTER:
      ty
    default:
      ty
  }
}

// hintAtAttributeType simply return the single argument unaltered.
// It is used to add enum name hint information for static analysis.
sub GLenum hintAtAttributeType(GLenum ty) {
    return switch ty {
    case GL_FLOAT,
        GL_FLOAT_VEC2,
        GL_FLOAT_VEC3,
        GL_FLOAT_VEC4,
        GL_FLOAT_MAT2,
        GL_FLOAT_MAT3,
        GL_FLOAT_MAT4,
        GL_FLOAT_MAT2x3,
        GL_FLOAT_MAT2x4,
        GL_FLOAT_MAT3x2,
        GL_FLOAT_MAT3x4,
        GL_FLOAT_MAT4x2,
        GL_FLOAT_MAT4x3,
        GL_INT,
        GL_INT_VEC2,
        GL_INT_VEC3,
        GL_INT_VEC4,
        GL_UNSIGNED_INT,
        GL_UNSIGNED_INT_VEC2,
        GL_UNSIGNED_INT_VEC3,
        GL_UNSIGNED_INT_VEC4,
        GL_DOUBLE,
        GL_DOUBLE_VEC2,
        GL_DOUBLE_VEC3,
        GL_DOUBLE_VEC4,
        GL_DOUBLE_MAT2,
        GL_DOUBLE_MAT3,
        GL_DOUBLE_MAT4,
        GL_DOUBLE_MAT2x3,
        GL_DOUBLE_MAT2x4,
        GL_DOUBLE_MAT3x2,
        GL_DOUBLE_MAT3x4,
        GL_DOUBLE_MAT4x2,
        GL_DOUBLE_MAT4x3:
      ty
    default:
      ty
  }
}
