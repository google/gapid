// Copyright (C) 2017 Google Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

@internal
class TransformFeedback {
  TransformFeedbackId ID

  // Table 21.35: Transform Feedback State
  ref!Buffer                  Buffer
  map!(GLuint, BufferBinding) Buffers
  GLboolean                   Paused  = GL_FALSE
  GLboolean                   Active  = GL_FALSE
  GLenum                      PrimitiveMode = GL_NONE
  @unused string              Label
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glBeginTransformFeedback.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glBeginTransformFeedback.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glBeginTransformFeedback.xhtml", Version.GLES32)
cmd void glBeginTransformFeedback(GLenum primitiveMode) {
  switch (primitiveMode) {
    case GL_LINES, GL_POINTS, GL_TRIANGLES: {
      // version 3.0
    }
    default: {
      glErrorInvalidEnum(primitiveMode)
    }
  }
  ctx := GetContext()
  ctx.Bound.TransformFeedback.Paused = GL_FALSE
  ctx.Bound.TransformFeedback.Active = GL_TRUE
  ctx.Bound.TransformFeedback.PrimitiveMode = primitiveMode
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glBindTransformFeedback.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glBindTransformFeedback.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glBindTransformFeedback.xhtml", Version.GLES32)
cmd void glBindTransformFeedback(GLenum target, TransformFeedbackId id) {
  ctx := GetContext()
  switch (target) {
    case GL_TRANSFORM_FEEDBACK: {
      // version 3.0
      ctx.Bound.TransformFeedback = GetOrCreateTransformFeedback(id)
    }
    default: {
      glErrorInvalidEnum(target)
    }
  }
}

sub ref!TransformFeedback GetOrCreateTransformFeedback(TransformFeedbackId id) {
  ctx := GetContext()
  if (id != 0) && (ctx.Objects.TransformFeedbacks[id] == null) {
    tf := new!TransformFeedback(ID: id)
    for i in as!GLuint(0) .. as!GLuint(ctx.Constants.MaxTransformFeedbackSeparateAttribs) {
      tf.Buffers[i] = BufferBinding()
    }
    ctx.Objects.TransformFeedbacks[id] = tf
    ctx.Objects.GeneratedNames.TransformFeedbacks[id] = true
  }
  return ctx.Objects.TransformFeedbacks[id]
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glDeleteTransformFeedbacks.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glDeleteTransformFeedbacks.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glDeleteTransformFeedbacks.xhtml", Version.GLES32)
cmd void glDeleteTransformFeedbacks(GLsizei count, const TransformFeedbackId* ids) {
  CheckCountGE!GLsizei(count, 0)
  tfs := ids[0:count]
  ctx := GetContext()
  for i in (0 .. count) {
    id := tfs[i]
    if id != 0 {
      delete(ctx.Objects.TransformFeedbacks, id)
      delete(ctx.Objects.GeneratedNames.TransformFeedbacks, id)
    }
  }
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glBeginTransformFeedback.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glBeginTransformFeedback.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glBeginTransformFeedback.xhtml", Version.GLES32)
cmd void glEndTransformFeedback() {
  ctx := GetContext()
  ctx.Bound.TransformFeedback.Paused = GL_FALSE
  ctx.Bound.TransformFeedback.Active = GL_FALSE
  ctx.Bound.TransformFeedback.PrimitiveMode = GL_NONE
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGenTransformFeedbacks.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGenTransformFeedbacks.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGenTransformFeedbacks.xhtml", Version.GLES32)
cmd void glGenTransformFeedbacks(GLsizei count, TransformFeedbackId* ids) {
  CheckCountGE!GLsizei(count, 0)
  tfs := ids[0:count]
  ctx := GetContext()
  for i in (0 .. count) {
    id := as!TransformFeedbackId(?)
    assert(id != 0)
    ctx.Objects.GeneratedNames.TransformFeedbacks[id] = true
    tfs[i] = id
  }
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGetTransformFeedbackVarying.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGetTransformFeedbackVarying.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGetTransformFeedbackVarying.xhtml", Version.GLES32)
cmd void glGetTransformFeedbackVarying(ProgramId program,
                                       GLuint    index,
                                       GLsizei   bufSize,
                                       GLsizei*  length,
                                       GLsizei*  size,
                                       GLenum*   type,
                                       GLchar*   name) {
  // TODO
  writeString(bufSize, length, name)
  size[0] = ?
  type[0] = ?
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glIsTransformFeedback.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glIsTransformFeedback.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glIsTransformFeedback.xhtml", Version.GLES32)
cmd GLboolean glIsTransformFeedback(TransformFeedbackId id) {
  ctx := GetContext()
  return toGLboolean(id in ctx.Objects.TransformFeedbacks)
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glPauseTransformFeedback.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glPauseTransformFeedback.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glPauseTransformFeedback.xhtml", Version.GLES32)
cmd void glPauseTransformFeedback() {
  ctx := GetContext()
  ctx.Bound.TransformFeedback.Paused = GL_TRUE
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glResumeTransformFeedback.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glResumeTransformFeedback.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glResumeTransformFeedback.xhtml", Version.GLES32)
cmd void glResumeTransformFeedback() {
  ctx := GetContext()
  ctx.Bound.TransformFeedback.Paused = GL_FALSE
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glTransformFeedbackVaryings.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glTransformFeedbackVaryings.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glTransformFeedbackVaryings.xhtml", Version.GLES32)
cmd void glTransformFeedbackVaryings(ProgramId            program,
                                     GLsizei              count,
                                     const GLchar* const* varyings,
                                     GLenum               bufferMode) {
  p := GetProgramOrError(program)
  switch (bufferMode) {
    case GL_INTERLEAVED_ATTRIBS, GL_SEPARATE_ATTRIBS: {
      p.TransformFeedbackBufferMode = bufferMode
    }
    default: {
      glErrorInvalidEnum(bufferMode)
    }
  }
  names := varyings[0:count]
  // TODO: Better syntax for clearing map.
  p.TransformFeedbackVaryings = Program().TransformFeedbackVaryings
  for i in (0 .. count) {
    name := as!string(as!char*(names[i])) // Read null-terminated string. TODO: Better syntax.
    p.TransformFeedbackVaryings[as!u32(i)] = name
  }
}
