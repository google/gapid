// Copyright (C) 2017 Google Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

type GLuint VertexBufferBindingIndex

// Vertex Array Object (VAO)
@internal
class VertexArray {
  VertexArrayId ID

  map!(VertexBufferBindingIndex, ref!VertexBufferBinding) VertexBufferBindings
  map!(AttributeLocation, ref!VertexAttributeArray)       VertexAttributeArrays

  // Table 21.3: Vertex Array Object State
  ref!Buffer     ElementArrayBuffer
  @unused string Label
}

@internal
class VertexAttributeArray {
  // Table 21.3: Vertex Array Object State
  // TODO: Some of the values here are just tracking state, but are effectively replaced by
  //       values in VertexBufferBinding. Do we want to represent it in the UI in some way?
  GLboolean                Enabled        = GL_FALSE
  GLint                    Size           = 4        // Number of components. 1, 2, 3 or 4.
  GLsizei                  Stride         = 0        // Obsolete/incorrect. See VertexBufferBinding stride instead.
  GLenum                   Type           = GL_FLOAT // Type of each component.
  GLboolean                Normalized     = GL_FALSE
  GLboolean                Integer        = GL_FALSE // Do not convert to float - the shader uses integer type.
  @unused GLuint           Divisor        = 0        // TODO: Set
  VertexPointer            Pointer                   // Client's data if BoundVertexArray == 0 && Buffer == 0
  @unused GLuint           BufferBinding  = 0        // TODO: Set
  VertexBufferBindingIndex Binding                   // Index of VertexBufferBinding.
  GLuint                   RelativeOffset = 0
}

@internal
class VertexBufferBinding {
  // Table 21.3: Vertex Array Object State
  GLintptr Offset  = 0
  GLsizei  Stride  = 16
  GLuint   Divisor = 0  // Non-zero values enable instancing.
  BufferId Buffer  = 0
}

@internal
class VertexAttributeValue {
  // Table 21.30: Vertex Shader State (not part of program objects)
  @unused u8[] Value // Used if VertexAttributeArray is disabled.
}

sub void CheckAttributeLocation(AttributeLocation location) {
  ctx := GetContext()
  CheckLocationLT!AttributeLocation(location, as!AttributeLocation(ctx.Constants.MaxVertexAttribs))
}

sub void CheckNonDefaultVertexArrayBound() {
  ctx := GetContext()
  if ctx.Bound.VertexArray.ID == 0 {
    glErrorInvalidOperationMsg(new!ERR_INVALID_OPERATION_DEFAULT_VERTEX_ARRAY_BOUND())
  }
}

sub bool IsDefaultVertexArrayBound() {
  ctx := GetContext()
  return ctx.Bound.VertexArray.ID == 0
}

sub ref!VertexArray GetOrCreateVertexArray(VertexArrayId id) {
  ctx := GetContext()
  if (id != 0) && (ctx.Objects.VertexArrays[id] == null) {
    ctx.Objects.VertexArrays[id] = NewVertexArray(id)
    ctx.Objects.GeneratedNames.VertexArrays[id] = true
  }
  return ctx.Objects.VertexArrays[id]
}

sub ref!VertexArray NewVertexArray(VertexArrayId id) {
  ctx := GetContext()
  array := new!VertexArray(ID: id)
  if VersionGreaterOrEqual(ctx, 3, 1) {
    for i in 0 .. as!VertexBufferBindingIndex(ctx.Constants.MaxVertexAttribBindings) {
      array.VertexBufferBindings[i] = new!VertexBufferBinding()
    }
  } else {
    for i in 0 .. as!VertexBufferBindingIndex(ctx.Constants.MaxVertexAttribs) {
      array.VertexBufferBindings[i] = new!VertexBufferBinding()
    }
  }
  for i in (0 .. as!AttributeLocation(ctx.Constants.MaxVertexAttribs)) {
    array.VertexAttributeArrays[i] = new!VertexAttributeArray(Binding: as!VertexBufferBindingIndex(i))
  }
  return array
}

// Logic shared by glGetVertexAttrib* commands.
sub u64 GetVertexAttrib(ref!Context ctx, AttributeLocation index, GLenum pname) {
  vao := ctx.Bound.VertexArray
  array := vao.VertexAttributeArrays[index]
  // TODO: Uncomment all the version checks
  return switch (pname) {
    case GL_VERTEX_ATTRIB_ARRAY_ENABLED:        { as!u64(array.Enabled) }
    case GL_VERTEX_ATTRIB_ARRAY_SIZE:           { as!u64(array.Size) }
    case GL_VERTEX_ATTRIB_ARRAY_TYPE:           { as!u64(array.Type) }
    case GL_VERTEX_ATTRIB_ARRAY_NORMALIZED:     { as!u64(array.Normalized) }
    case GL_VERTEX_ATTRIB_ARRAY_STRIDE:         { as!u64(array.Stride) }
    case GL_VERTEX_ATTRIB_ARRAY_BUFFER_BINDING: {
      as!u64(vao.VertexBufferBindings[array.Binding].Buffer)
    }
    @if(Version.GLES30)
    case GL_VERTEX_ATTRIB_ARRAY_DIVISOR: {
      as!u64(vao.VertexBufferBindings[array.Binding].Divisor)
    }
    @if(Version.GLES30)
    case GL_VERTEX_ATTRIB_ARRAY_INTEGER: {
      as!u64(array.Integer)
    }
    @if(Version.GLES31)
    case GL_VERTEX_ATTRIB_BINDING: {
      as!u64(array.Binding)
    }
    @if(Version.GLES31)
    case GL_VERTEX_ATTRIB_RELATIVE_OFFSET: { // SPEC: missing in html
      as!u64(array.RelativeOffset)
    }
    default: {
      // glErrorInvalidEnum(pname)
      0
    }
  }
}

sub GLint VertexAttribTypeSize(GLenum type) {
  return switch (type) {
    case GL_BYTE, GL_UNSIGNED_BYTE:                                     1
    case GL_SHORT, GL_UNSIGNED_SHORT, GL_HALF_FLOAT, GL_HALF_FLOAT_OES: 2
    case GL_INT, GL_UNSIGNED_INT, GL_FLOAT, GL_FIXED:                   4
    case GL_INT_2_10_10_10_REV, GL_UNSIGNED_INT_2_10_10_10_REV:         4
  }
}

sub void ReadVertexArrays(ref!Context ctx, u32 first_index, u32 index_count, u32 instance_count) {
  if (index_count > 0) && (instance_count > 0) {
    // Only the default vertex array can contain client data pointer.
    if IsDefaultVertexArrayBound() {
      vao := ctx.Objects.VertexArrays[0]
      for i in (0 .. as!AttributeLocation(ctx.Constants.MaxVertexAttribs)) {
        arr := vao.VertexAttributeArrays[i]
        if arr.Enabled == GL_TRUE {
          binding := vao.VertexBufferBindings[arr.Binding]
          if (binding.Buffer == null) && (arr.Pointer != null) {
            stride := binding.Stride
            size := VertexAttribTypeSize(arr.Type) * arr.Size
            divisor := as!u32(binding.Divisor)
            if divisor == 0 {
              for v in (first_index .. (first_index + index_count)) {
                offset := as!GLint(stride) * as!GLint(v)
                read(arr.Pointer[offset:offset + size])
              }
            } else {
              last_instance := instance_count - 1
              last_index := last_instance / divisor
              // First index is not added to instanced arrays.
              for v in (0 .. (last_index + 1)) {
                offset := as!GLint(stride) * as!GLint(v)
                read(arr.Pointer[offset:offset + size])
              }
            }
          }
        }
      }
    }
  }
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glBindVertexArray.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glBindVertexArray.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glBindVertexArray.xhtml", Version.GLES32)
cmd void glBindVertexArray(VertexArrayId array) {
  BindVertexArray(array)
}

sub void BindVertexArray(VertexArrayId array) {
  ctx := GetContext()
  ctx.Bound.VertexArray = GetOrCreateVertexArray(array)
}

sub void BindVertexBuffer(ref!Context ctx, VertexBufferBindingIndex binding_index, BufferId buffer, GLintptr offset, GLsizei stride) {
  CheckGE!GLintptr(offset, 0)
  CheckGE!GLsizei(stride, 0)
  if VersionGreaterOrEqual(ctx, 3, 1) {
    CheckLT!VertexBufferBindingIndex(binding_index, as!VertexBufferBindingIndex(ctx.Constants.MaxVertexAttribBindings))
    CheckLT!GLsizei(stride, as!GLsizei(ctx.Constants.MaxVertexAttribStride))
  }
  _ = GetOrCreateBuffer(buffer)
  vao := ctx.Bound.VertexArray
  binding := vao.VertexBufferBindings[binding_index]
  binding.Buffer = buffer
  binding.Offset = offset
  binding.Stride = stride
}

@if(Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glBindVertexBuffer.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glBindVertexBuffer.xhtml", Version.GLES32)
cmd void glBindVertexBuffer(VertexBufferBindingIndex binding_index, BufferId buffer, GLintptr offset, GLsizei stride) {
  ctx := GetContext()
  CheckNonDefaultVertexArrayBound()
  BindVertexBuffer(ctx, binding_index, buffer, offset, stride)
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glDeleteVertexArrays.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glDeleteVertexArrays.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glDeleteVertexArrays.xhtml", Version.GLES32)
cmd void glDeleteVertexArrays(GLsizei count, const VertexArrayId* arrays) {
  DeleteVertexArrays(count, arrays)
}

sub void DeleteVertexArrays(GLsizei count, const VertexArrayId* arrays) {
  CheckCountGE!GLsizei(count, 0)
  ctx := GetContext()
  a := arrays[0:count]
  for i in (0 .. count) {
    id := a[i]
    // Silently ignore invalid ids
    if id != 0 {
      if id in ctx.Objects.VertexArrays {
        delete(ctx.Objects.VertexArrays, id)
        delete(ctx.Objects.GeneratedNames.VertexArrays, id)
        if ctx.Bound.VertexArray.ID == id {
          ctx.Bound.VertexArray = ctx.Objects.Default.VertexArray
        }
      }
    }
  }
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glEnableVertexAttribArray.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glEnableVertexAttribArray.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glEnableVertexAttribArray.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glEnableVertexAttribArray.xhtml", Version.GLES32)
cmd void glDisableVertexAttribArray(AttributeLocation location) {
  ctx := GetContext()
  CheckAttributeLocation(location)
  vao := ctx.Bound.VertexArray
  vao.VertexAttributeArrays[location].Enabled = GL_FALSE
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glEnableVertexAttribArray.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glEnableVertexAttribArray.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glEnableVertexAttribArray.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glEnableVertexAttribArray.xhtml", Version.GLES32)
cmd void glEnableVertexAttribArray(AttributeLocation location) {
  ctx := GetContext()
  CheckAttributeLocation(location)
  vao := ctx.Bound.VertexArray
  vao.VertexAttributeArrays[location].Enabled = GL_TRUE
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGenVertexArrays.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGenVertexArrays.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGenVertexArrays.xhtml", Version.GLES32)
cmd void glGenVertexArrays(GLsizei count, VertexArrayId* arrays) {
  GenVertexArrays(count, arrays)
}

sub void GenVertexArrays(GLsizei count, VertexArrayId* arrays) {
  CheckCountGE!GLsizei(count, 0)
  ctx := GetContext()
  a := arrays[0:count]
  for i in (0 .. count) {
    id := as!VertexArrayId(?)
    assert(id != 0)
    ctx.Objects.GeneratedNames.VertexArrays[id] = true
    a[i] = id
  }
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGetVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGetVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGetVertexAttrib.xhtml", Version.GLES32)
cmd void glGetVertexAttribIiv(AttributeLocation index, GLenum pname, GLint* params) {
  ctx := GetContext()
  CheckAttributeLocation(index)
  if pname == GL_CURRENT_VERTEX_ATTRIB {
    // attr := ctx.Vertex.Attributes[index]
    // TODO: params[0:4] = as!GLint[](attr.Value)[0:4]
    write(params[0:4])
  } else {
    params[0] = as!GLint(GetVertexAttrib(ctx, index, pname))
  }
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGetVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGetVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGetVertexAttrib.xhtml", Version.GLES32)
cmd void glGetVertexAttribIuiv(AttributeLocation index, GLenum pname, GLuint* params) {
  ctx := GetContext()
  CheckAttributeLocation(index)
  if pname == GL_CURRENT_VERTEX_ATTRIB {
    // attr := ctx.Vertex.Attributes[index]
    // TODO: params[0:4] = as!GLuint[](attr.Value)[0:4]
    write(params[0:4])
  } else {
    params[0] = as!GLuint(GetVertexAttrib(ctx, index, pname))
  }
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glGetVertexAttribPointerv.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGetVertexAttribPointerv.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGetVertexAttribPointerv.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGetVertexAttribPointerv.xhtml", Version.GLES32)
cmd void glGetVertexAttribPointerv(AttributeLocation index, GLenum pname, void** pointer) {
  ctx := GetContext()
  CheckAttributeLocation(index)
  if pname != GL_VERTEX_ATTRIB_ARRAY_POINTER { glErrorInvalidEnum(pname) }
  vao := ctx.Bound.VertexArray
  pointer[0] = as!void*(vao.VertexAttributeArrays[index].Pointer)
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glGetVertexAttrib.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGetVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGetVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGetVertexAttrib.xhtml", Version.GLES32)
cmd void glGetVertexAttribfv(AttributeLocation index, GLenum pname, GLfloat* params) {
  ctx := GetContext()
  CheckAttributeLocation(index)
  if pname == GL_CURRENT_VERTEX_ATTRIB {
    // TODO: params[0:4] = as!GLfloat[](attr.Value)[0:4]
    // attr := ctx.Vertex.Attributes[index]
    write(params[0:4])
  } else {
    params[0] = as!GLfloat(GetVertexAttrib(ctx, index, pname))
  }
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glGetVertexAttrib.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glGetVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glGetVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glGetVertexAttrib.xhtml", Version.GLES32)
cmd void glGetVertexAttribiv(AttributeLocation index, GLenum pname, GLint* params) {
  ctx := GetContext()
  CheckAttributeLocation(index)
  if pname == GL_CURRENT_VERTEX_ATTRIB {
    // TODO: params[0:4] = as!GLint[](attr.Value)[0:4]
    // attr := ctx.Vertex.Attributes[index]
    write(params[0:4])
  } else {
    params[0] = as!GLint(GetVertexAttrib(ctx, index, pname))
  }
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glIsVertexArray.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glIsVertexArray.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glIsVertexArray.xhtml", Version.GLES32)
cmd GLboolean glIsVertexArray(VertexArrayId array) {
  return IsVertexArray(array)
}

sub GLboolean IsVertexArray(VertexArrayId array) {
  ctx := GetContext()
  return as!GLboolean((array != 0) && (array in ctx.Objects.VertexArrays))
}

sub void VertexAttribF(AttributeLocation location, Vec4f value) {
  ctx := GetContext()
  CheckAttributeLocation(location)
  vals := make!Vec4f(1)
  vals[0] = value
  ctx.Vertex.Attributes[location] = VertexAttributeValue(Value: as!u8[](vals))
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glVertexAttrib.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttrib1f(AttributeLocation location, GLfloat value0) {
  VertexAttribF(location, Vec4f(value0, 0.0, 0.0, 1.0))
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glVertexAttrib.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttrib1fv(AttributeLocation location, const GLfloat* value) {
  v := value[0:1]
  vec := Vec4f(v[0], 0.0, 0.0, 1.0)
  VertexAttribF(location, vec)
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glVertexAttrib.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttrib2f(AttributeLocation location, GLfloat value0, GLfloat value1) {
  VertexAttribF(location, Vec4f(value0, value1, 0.0, 1.0))
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glVertexAttrib.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttrib2fv(AttributeLocation location, const GLfloat* value) {
  v := value[0:2]
  vec := Vec4f(v[0], v[1], 0.0, 1.0)
  VertexAttribF(location, vec)
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glVertexAttrib.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttrib3f(AttributeLocation location,
                          GLfloat           value0,
                          GLfloat           value1,
                          GLfloat           value2) {
  VertexAttribF(location, Vec4f(value0, value1, value2, 1.0))
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glVertexAttrib.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttrib3fv(AttributeLocation location, const GLfloat* value) {
  v := value[0:3]
  vec := Vec4f(v[0], v[1], v[2], 1.0)
  VertexAttribF(location, vec)
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glVertexAttrib.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttrib4f(AttributeLocation location,
                          GLfloat           value0,
                          GLfloat           value1,
                          GLfloat           value2,
                          GLfloat           value3) {
  VertexAttribF(location, Vec4f(value0, value1, value2, value3))
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glVertexAttrib.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttrib4fv(AttributeLocation location, const GLfloat* value) {
  v := value[0:4]
  vec := Vec4f(v[0], v[1], v[2], v[3])
  VertexAttribF(location, vec)
}

sub void VertexAttribBinding(ref!Context ctx, AttributeLocation index, VertexBufferBindingIndex binding_index) {
  if VersionGreaterOrEqual(ctx, 3, 1) {
    CheckAttributeLocation(index)
    CheckLT!VertexBufferBindingIndex(binding_index, as!VertexBufferBindingIndex(ctx.Constants.MaxVertexAttribBindings))
  }
  // NB: Some callers may use the default VertexArray and some may not.
  //     Therefore the caller should do the check if it is needed.
  vao := ctx.Bound.VertexArray
  vao.VertexAttributeArrays[index].Binding = binding_index
}

@if(Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttribBinding.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttribBinding.xhtml", Version.GLES32)
cmd void glVertexAttribBinding(AttributeLocation index, VertexBufferBindingIndex binding_index) {
  ctx := GetContext()
  CheckNonDefaultVertexArrayBound()
  VertexAttribBinding(ctx, index, binding_index)
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttribDivisor.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttribDivisor.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttribDivisor.xhtml", Version.GLES32)
cmd void glVertexAttribDivisor(AttributeLocation index, GLuint divisor) {
  VertexAttribDivisor(index, divisor)
}

sub void VertexAttribDivisor(AttributeLocation index, GLuint divisor) {
  ctx := GetContext()
  binding_index := as!VertexBufferBindingIndex(index)
  VertexAttribBinding(ctx, index, binding_index)
  vao := ctx.Bound.VertexArray
  vao.VertexBufferBindings[binding_index].Divisor = divisor
}

sub void VertexAttribFormat(ref!Context       ctx,
                            AttributeLocation index,
                            GLint             size,
                            GLenum            type,
                            GLboolean         normalized,
                            GLuint            relativeOffset,
                            bool              integer) {
  CheckAttributeLocation(index)
  if !((1 <= size) && (size <= 4)) { glErrorInvalidValue!GLint(size) }
  if (integer) {
    switch (type) {
      @if(Version.GLES30)
      case GL_BYTE, GL_INT, GL_SHORT, GL_UNSIGNED_BYTE, GL_UNSIGNED_INT, GL_UNSIGNED_SHORT: {
      }
      default: {
        glErrorInvalidEnum(type)
      }
    }
  } else {
    switch (type) {
      @if(Version.GLES20)
      case GL_BYTE, GL_FIXED, GL_FLOAT, GL_SHORT, GL_UNSIGNED_BYTE, GL_UNSIGNED_SHORT: {
      }
      @if(Extension.GL_OES_vertex_half_float)
      case GL_HALF_FLOAT_OES: {
      }
      @if(Version.GLES30)
      case GL_HALF_FLOAT, GL_INT, GL_UNSIGNED_INT: {
      }
      @if(Version.GLES30)
      case GL_INT_2_10_10_10_REV, GL_UNSIGNED_INT_2_10_10_10_REV: {
        if size != 4 { glErrorInvalidOperation() }
      }
      default: {
        glErrorInvalidEnum(type)
      }
    }
  }
  // NB: Some callers may use the default VertexArray and some may not.
  //     Therefore the caller should do the check if it is needed.
  if VersionGreaterOrEqual(ctx, 3, 1) {
    CheckLE!GLuint(relativeOffset, as!GLuint(ctx.Constants.MaxVertexAttribRelativeOffset))
  }
  vao := ctx.Bound.VertexArray
  format := vao.VertexAttributeArrays[index]
  format.Size = size
  format.Type = type
  if (!integer) {
    format.Normalized = normalized
  }
  format.RelativeOffset = relativeOffset
  format.Integer = as!GLboolean(integer)
}

@if(Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttribFormat.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttribFormat.xhtml", Version.GLES32)
cmd void glVertexAttribFormat(AttributeLocation index,
                              GLint             size,
                              GLenum            type,
                              GLboolean         normalized,
                              GLuint            relativeoffset) {
  ctx := GetContext()
  CheckNonDefaultVertexArrayBound()
  VertexAttribFormat(ctx, index, size, type, normalized, relativeoffset, false)
}

sub void VertexAttribI(AttributeLocation location, Vec4i value) {
  ctx := GetContext()
  CheckAttributeLocation(location)
  vals := make!Vec4i(1)
  vals[0] = value
  ctx.Vertex.Attributes[location] = VertexAttributeValue(Value: as!u8[](vals))
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttribI4i(AttributeLocation index, GLint x, GLint y, GLint z, GLint w) {
  VertexAttribI(index, Vec4i(x, y, z, w))
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttribI4iv(AttributeLocation index, const GLint* values) {
  v := values[0:4]
  vec := Vec4i(v[0], v[1], v[2], v[3])
  VertexAttribI(index, vec)
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttribI4ui(AttributeLocation index, GLuint x, GLuint y, GLuint z, GLuint w) {
  VertexAttribI(index, Vec4i(as!GLint(x), as!GLint(y), as!GLint(z), as!GLint(w)))
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttrib.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttrib.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttrib.xhtml", Version.GLES32)
cmd void glVertexAttribI4uiv(AttributeLocation index, const GLuint* values) {
  v := values[0:4]
  vec := Vec4i(as!GLint(v[0]), as!GLint(v[1]), as!GLint(v[2]), as!GLint(v[3]))
  VertexAttribI(index, vec)
}

@if(Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttribFormat.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttribFormat.xhtml", Version.GLES32)
cmd void glVertexAttribIFormat(AttributeLocation index, GLint size, GLenum type, GLuint relativeoffset) {
  ctx := GetContext()
  CheckNonDefaultVertexArrayBound()
  VertexAttribFormat(ctx, index, size, type, as!GLboolean(0), relativeoffset, true)
}

sub void VertexAttribPointer(ref!Context       ctx,
                             AttributeLocation index,
                             GLint             size,
                             GLenum            type,
                             GLboolean         normalized,
                             GLsizei           stride,
                             VertexPointer     pointer,
                             bool              integer) {
  boundArrayBuffer := ctx.Bound.ArrayBuffer
  // Do not allow use of client's memory for non-default vertex array.
  if (!IsDefaultVertexArrayBound()) && (boundArrayBuffer == null) && (pointer != null) { glErrorInvalidOperation() }
  VertexAttribFormat(ctx, index, size, type, normalized, 0, integer)
  binding_index := as!VertexBufferBindingIndex(index)
  VertexAttribBinding(ctx, index, binding_index)
  vertexTypeSize := VertexAttribTypeSize(type)
  effectiveStride := switch (stride != 0) {
    case true:  stride
    case false: as!GLsizei(vertexTypeSize * size)
  }
  vao := ctx.Bound.VertexArray
  vao.VertexAttributeArrays[index].Stride = stride
  vao.VertexAttributeArrays[index].Pointer = pointer
  if IsDefaultVertexArrayBound() && (boundArrayBuffer == null) {
    // Use client's memory as data buffer - this is the only way to set it.
    BindVertexBuffer(ctx, binding_index, 0, 0, effectiveStride)
  } else {
    // This call does not allow use of client's memory due to check at the top of function,
    // although it is possible to clear the buffer later while keeping pointer set.
    // I wonder how drivers handle it.  We just ignore non-default vertex arrays.
    offset := as!GLintptr(as!u64(pointer))
    boundArrayBufferId := GetID!(BufferId,Buffer)(boundArrayBuffer)
    BindVertexBuffer(ctx, binding_index, boundArrayBufferId, offset, effectiveStride)
  }
}

@if(Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttribPointer.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttribPointer.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttribPointer.xhtml", Version.GLES32)
cmd void glVertexAttribIPointer(AttributeLocation location,
                                GLint             size,
                                GLenum            type,
                                GLsizei           stride,
                                VertexPointer     data) {
  ctx := GetContext()
  VertexAttribPointer(ctx, location, size, type, as!GLboolean(0), stride, data, true)
}

@if(Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man/xhtml/glVertexAttribPointer.xml", Version.GLES20)
@doc("https://www.khronos.org/opengles/sdk/docs/man3/html/glVertexAttribPointer.xhtml", Version.GLES30)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexAttribPointer.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexAttribPointer.xhtml", Version.GLES32)
cmd void glVertexAttribPointer(AttributeLocation location,
                               GLint             size,
                               GLenum            type,
                               GLboolean         normalized,
                               GLsizei           stride,
                               VertexPointer     data) {
  ctx := GetContext()
  VertexAttribPointer(ctx, location, size, type, normalized, stride, data, false)
}

@if(Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man31/html/glVertexBindingDivisor.xhtml", Version.GLES31)
@doc("https://www.khronos.org/opengles/sdk/docs/man32/html/glVertexBindingDivisor.xhtml", Version.GLES32)
cmd void glVertexBindingDivisor(VertexBufferBindingIndex binding_index, GLuint divisor) {
  ctx := GetContext()
  CheckLT!VertexBufferBindingIndex(binding_index, as!VertexBufferBindingIndex(ctx.Constants.MaxVertexAttribBindings))
  CheckNonDefaultVertexArrayBound()
  vao := ctx.Bound.VertexArray
  vao.VertexBufferBindings[binding_index].Divisor = divisor
}
