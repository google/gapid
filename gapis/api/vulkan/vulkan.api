// Copyright (C) 2017 Google Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// Based off of the original vulkan.h header file which has the following
// license.

// Copyright (c) 2015 The Khronos Group Inc.
//
// Permission is hereby granted, free of charge, to any person obtaining a
// copy of this software and/or associated documentation files (the
// "Materials"), to deal in the Materials without restriction, including
// without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Materials, and to
// permit persons to whom the Materials are furnished to do so, subject to
// the following conditions:
//
// The above copyright notice and this permission notice shall be included
// in all copies or substantial portions of the Materials.
//
// THE MATERIALS ARE PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
// EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
// MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
// CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
// TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// MATERIALS OR THE USE OR OTHER DEALINGS IN THE MATERIALS.

api_index 2

import "api/constants.api"
import "api/bitfields.api"
import "api/enums.api"
import "api/types.api"
import "api/structs.api"
import "api/vk11structs.api"

import "api/properties_features_requirements.api"
import "api/instance.api"
import "api/physical_device.api"
import "api/device.api"
import "api/queue.api"
import "api/memory.api"
import "api/coherent_memory.api"
import "api/image.api"
import "api/buffer.api"
import "api/descriptor.api"
import "api/command_buffer_control.api"
import "api/copy_clear_commands.api"
import "api/draw_commands.api"
import "api/pipeline.api"
import "api/query_pool.api"
import "api/renderpass_framebuffer.api"
import "api/synchronization.api"
import "api/queued_command_tracking.api"
import "api/util.api"

import "extensions/ext_debug_marker.api"
import "extensions/ext_debug_report.api"
import "extensions/khr_dedicated_allocation.api"
import "extensions/khr_display.api"
import "extensions/khr_display_swapchain.api"
import "extensions/khr_get_memory_requirements2.api"
import "extensions/khr_get_physical_device_properties2.api"
import "extensions/khr_get_surface_capabilities2.api"
import "extensions/khr_surface.api"
import "extensions/khr_swapchain.api"
import "extensions/nv_dedicated_allocation.api"

import "android/vulkan_android.api"
import "linux/vulkan_linux.api"
import "windows/vulkan_windows.api"
import "synthetic.api"
import "errors.api"

////////////////////////
//  Extern functions  //
////////////////////////

extern void mapMemory(void** mem, u8[] slice)
extern void unmapMemory(u8[] slice)
extern void trackMappedCoherentMemory(u64 start, size size)
extern void readMappedCoherentMemory(VkDeviceMemory memory, u64 offset_in_mapped, size readSize)
extern void untrackMappedCoherentMemory(u64 start, size size)
extern bool hasDynamicProperty(const VkPipelineDynamicStateCreateInfo* info, VkDynamicState state)
extern void notifyPendingCommandAdded(VkQueue queue)
extern void enterSubcontext()
extern void nextSubcontext()
extern void leaveSubcontext()
extern void resetSubcontext()
extern void pushDebugMarker(string name)
extern void popDebugMarker()
extern void pushRenderPassMarker(VkRenderPass renderPass)
extern void popRenderPassMarker()
extern void popAndPushMarkerForNextSubpass(u32 nextSubpass)
extern void postBindSparse(ref!QueuedSparseBinds binds)
extern void onPreSubcommand(ref!CommandReference ref)
extern void onPreProcessCommand(ref!CommandReference ref)
extern void onPostSubcommand(ref!CommandReference ref)
extern void onDeferSubcommand(ref!CommandReference ref)
extern void onCommandAdded(VkCommandBuffer buffer)
extern void resetCmd(VkCommandBuffer buffer)
extern void validate(string layerName, bool condition, string message)
extern void fetchPhysicalDeviceProperties(VkInstance instance, VkPhysicalDevice[] devs, ref!PhyDevProperties props)

///////////////////////
// Function pointers //
///////////////////////
// TODO: add support for function pointers.
@external type void* PFN_vkAllocationFunction
@pfn cmd void* vkAllocationFunction(
    void*                   pUserData,
    size                    size,
    size                    alignment,
    VkSystemAllocationScope allocationScope) {
  return ?
}

@external type void* PFN_vkReallocationFunction
@pfn cmd void* vkReallocationFunction(
    void*                   pUserData,
    void*                   pOriginal,
    size                    size,
    size                    alignment,
    VkSystemAllocationScope allocationScope) {
  return ?
}

@external type void* PFN_vkFreeFunction
@pfn cmd void vkFreeFunction(
    void* pUserData,
    void* pMemory) {
}

@external type void* PFN_vkInternalAllocationNotification
@pfn cmd void vkInternalAllocationNotification(
    void*                    pUserData,
    size                     size,
    VkInternalAllocationType allocationType,
    VkSystemAllocationScope  allocationScope) {
}

@external type void* PFN_vkInternalFreeNotification
@pfn cmd void vkInternalFreeNotification(
    void*                    pUserData,
    size                     size,
    VkInternalAllocationType allocationType,
    VkSystemAllocationScope  allocationScope) {
}

@external type void* PFN_vkVoidFunction
@pfn cmd void vkVoidFunction() {
}

//////////////////////////
// Supported extensions //
//////////////////////////

@internal
class ExtensionSet {
  map!(string, bool) ExtensionNames
}

sub ref!ExtensionSet supportedInstanceExtensions() {
  supported := new!ExtensionSet()
  // Recognized instance extensions
  supported.ExtensionNames["VK_KHR_surface"] = true
  supported.ExtensionNames["VK_KHR_display"] = true
  supported.ExtensionNames["VK_EXT_debug_report"] = true
  supported.ExtensionNames["VK_KHR_xlib_surface"] = true
  supported.ExtensionNames["VK_KHR_xcb_surface"] = true
  supported.ExtensionNames["VK_KHR_wayland_surface"] = true
  supported.ExtensionNames["VK_KHR_mir_surface"] = true
  supported.ExtensionNames["VK_KHR_android_surface"] = true
  supported.ExtensionNames["VK_KHR_win32_surface"] = true
  supported.ExtensionNames["VK_KHR_get_physical_device_properties2"] = true
  supported.ExtensionNames["Vk_KHR_get_surface_capabilities2"] = true
  return supported
}

sub ref!ExtensionSet supportedDeviceExtensions() {
  supported := new!ExtensionSet()
  // Recognized device extensions
  supported.ExtensionNames["VK_KHR_swapchain"] = true
  supported.ExtensionNames["VK_KHR_display_swapchain"] = true
  supported.ExtensionNames["VK_EXT_debug_marker"] = true
  supported.ExtensionNames["VK_KHR_ANDROID_native_buffer"] = true
  supported.ExtensionNames["VK_NV_dedicated_allocation"] = true
  supported.ExtensionNames["VK_KHR_get_memory_requirements2"] = true
  supported.ExtensionNames["VK_KHR_dedicated_allocation"] = true
  return supported
}

////////////////////////
// Draw info tracking //
////////////////////////

// This contains the draw command parameters. Only one of the draw data should be
// valid at a time. Others should be null.
@internal class DrawParameters {
  ref!vkCmdDrawArgs                Draw
  ref!vkCmdDrawIndexedArgs         DrawIndexed
  ref!vkCmdDrawIndirectArgs        DrawIndirect
  ref!vkCmdDrawIndexedIndirectArgs DrawIndexedIndirect
}

// This contains the information about a draw
@internal class DrawInfo {
  ref!FramebufferObject Framebuffer
  // A mapping from the descriptor set bound numbers to descriptor set objects
  map!(u32, ref!DescriptorSetObject) DescriptorSets
  // The Last subpass number used in the draw
  @hidden u32 LastSubpass
  // The graphics pipeline used for the draw
  ref!GraphicsPipelineObject GraphicsPipeline
  // The vertex buffers used for the draw. This is a map of binding number to
  // buffer bound do that binding.
  map!(u32, BoundBuffer) BoundVertexBuffers
  // The index buffer used for the draw
  ref!BoundIndexBuffer BoundIndexBuffer
  // The draw parameters used for the draw
  DrawParameters CommandParameters
  // The render pass in which this draw takes place
  ref!RenderPassObject RenderPass
  // Whether or not we are in an unclosed render pass
  @hidden bool InRenderPass
  // BufferBindingOffsets[setNum][bindingNum][bufferBindingNum] :=
  //    buffer offset for given descriptor set number, binding number, and index of buffer binding
  map!(u32, map!(u32, map!(u32, VkDeviceSize))) BufferBindingOffsets
}

@internal class ComputeInfo {
  // A mapping from the descriptor set bound numbers to descriptor set objects
  map!(u32, ref!DescriptorSetObject) DescriptorSets
  // The compute pipeline used for the draw
  ref!ComputePipelineObject ComputePipeline
  // BufferBindingOffsets[setNum][bindingNum][bufferBindingNum] :=
  //    buffer offset for given descriptor set number, binding number, and index of buffer binding
  map!(u32, map!(u32, map!(u32, VkDeviceSize))) BufferBindingOffsets
}

@internal class PresentInfo {
  // The number of images presented last present
  u32 PresentImageCount
  // The images presented in the last present
  map!(u32, ref!ImageObject) PresentImages
}

enum LastSubmissionType {
  SUBMIT  = 0
  PRESENT = 1
}

/////////////////////////////
// Internal State Tracking //
/////////////////////////////

// Dispatchable objects.
@serialize map!(VkInstance, ref!InstanceObject)             Instances
@serialize map!(VkPhysicalDevice, ref!PhysicalDeviceObject) PhysicalDevices
@serialize map!(VkDevice, ref!DeviceObject)                 Devices
@serialize map!(VkQueue, ref!QueueObject)                   Queues
@serialize map!(VkCommandBuffer, ref!CommandBufferObject)   CommandBuffers
// Non-dispatchable objects.
@serialize map!(VkDeviceMemory, ref!DeviceMemoryObject)               DeviceMemories
@serialize map!(VkBuffer, ref!BufferObject)                           Buffers
@serialize map!(VkBufferView, ref!BufferViewObject)                   BufferViews
@serialize map!(VkImage, ref!ImageObject)                             Images
@serialize map!(VkImageView, ref!ImageViewObject)                     ImageViews
@serialize map!(VkShaderModule, ref!ShaderModuleObject)               ShaderModules
@serialize map!(VkPipeline, ref!GraphicsPipelineObject)               GraphicsPipelines
@serialize map!(VkPipeline, ref!ComputePipelineObject)                ComputePipelines
@serialize map!(VkPipelineLayout, ref!PipelineLayoutObject)           PipelineLayouts
@serialize map!(VkSampler, ref!SamplerObject)                         Samplers
@serialize map!(VkDescriptorSet, ref!DescriptorSetObject)             DescriptorSets
@serialize map!(VkDescriptorSetLayout, ref!DescriptorSetLayoutObject) DescriptorSetLayouts
@serialize map!(VkDescriptorPool, ref!DescriptorPoolObject)           DescriptorPools
@serialize map!(VkFence, ref!FenceObject)                             Fences
@serialize map!(VkSemaphore, ref!SemaphoreObject)                     Semaphores
@serialize map!(VkEvent, ref!EventObject)                             Events
@serialize map!(VkQueryPool, ref!QueryPoolObject)                     QueryPools
@serialize map!(VkFramebuffer, ref!FramebufferObject)                 Framebuffers
@serialize map!(VkRenderPass, ref!RenderPassObject)                   RenderPasses
@serialize map!(VkPipelineCache, ref!PipelineCacheObject)             PipelineCaches
@serialize map!(VkCommandPool, ref!CommandPoolObject)                 CommandPools
@serialize map!(VkSurfaceKHR, ref!SurfaceObject)                      Surfaces
@serialize map!(VkSwapchainKHR, ref!SwapchainObject)                  Swapchains
@serialize map!(VkDisplayModeKHR, ref!DisplayModeObject)              DisplayModes
// Other state Tracking
@hidden @serialize map!(VkDevice, VkMemoryRequirements) TransferBufferMemoryRequirements
@serialize ref!QueueObject                              LastBoundQueue
@serialize map!(VkQueue, ref!DrawInfo)                  LastDrawInfos
@serialize map!(VkQueue, ref!ComputeInfo)               LastComputeInfos
@serialize PresentInfo                                  LastPresentInfo
@serialize LastSubmissionType                           LastSubmission

@hidden bool IsRebuilding

sub ref!DrawInfo lastDrawInfo() {
  if LastBoundQueue != null {
    if !(LastBoundQueue.VulkanHandle in LastDrawInfos) {
      LastDrawInfos[LastBoundQueue.VulkanHandle] = new!DrawInfo()
    }
  }

  ldi := switch (LastBoundQueue == null) {
    case false:
      LastDrawInfos[LastBoundQueue.VulkanHandle]
    case true:
      new!DrawInfo()
  }
  return ldi
}

// Clear the recorded descriptor sets in the last draw
sub void clearLastDrawInfoDescriptorSets() {
  for i in (0 .. len(lastDrawInfo().DescriptorSets)) {
    lastDrawInfo().DescriptorSets[as!u32(i)] = null
  }
}

// Clear the draw command parameters in the last draw info
sub void clearLastDrawInfoDrawCommandParameters() {
  lastDrawInfo().CommandParameters.Draw = null
  lastDrawInfo().CommandParameters.DrawIndexed = null
  lastDrawInfo().CommandParameters.DrawIndirect = null
  lastDrawInfo().CommandParameters.DrawIndexedIndirect = null
}

sub ref!ComputeInfo lastComputeInfo() {
  if LastBoundQueue != null {
    if !(LastBoundQueue.VulkanHandle in LastComputeInfos) {
      LastComputeInfos[LastBoundQueue.VulkanHandle] = new!ComputeInfo()
    }
  }

  lci := switch (LastBoundQueue == null) {
    case false:
      LastComputeInfos[LastBoundQueue.VulkanHandle]
    case true:
      new!ComputeInfo()
  }
  return lci
}
