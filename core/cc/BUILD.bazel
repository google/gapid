# Copyright (C) 2018 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("//:version.bzl", "agi_version")
load("//tools/build:rules.bzl", "cc_copts")

cc_library(
    name = "cc",
    srcs = glob(
        ["*.cpp"],
        exclude = [
            "*_test.cpp",
        ],
    ) + select({
        "//tools/build:linux": glob([
            "linux/*.cpp",
            "posix/*.cpp",
        ]),
        "//tools/build:fuchsia-arm64": glob([
            "fuchsia/*.cpp",
            "fuchsia/*.h",
            "posix/*.cpp",
        ]),
        "//tools/build:darwin": glob([
            "osx/*.cpp",
            "posix/*.cpp",
        ]),
        "//tools/build:windows": glob(["windows/*.cpp"]),
        "//conditions:default": glob([
            "android/*.cpp",
            "android/*.h",
            "posix/*.cpp",
        ]),
    }),
    hdrs = glob(["*.h"]),
    copts = cc_copts(),
    linkopts = select({
        "//tools/build:linux": ["-ldl"],
        "//tools/build:darwin": [],
        "//tools/build:fuchsia-arm64": [],
        "//tools/build:windows": [
            "-lws2_32",
            "-lshlwapi",
        ],
        # Android.
        "//conditions:default": ["-ldl"],
    }),
    visibility = ["//visibility:public"],
    deps = [
        "@cityhash",
    ] + select({
        # Turn off breakpad dependency on Fuchsia.
        "//tools/build:fuchsia-arm64": [],
        "//conditions:default": ["@breakpad"],
    }),
)

agi_version(
    name = "version",
    out = "version.h",
    template = "version.h.in",
    visibility = ["//visibility:public"],
)

cc_test(
    name = "tests",
    size = "small",
    srcs = [
        "connection_test.cpp",
        "crash_handler_test.cpp",
        "interval_list_test.cpp",
    ],
    copts = cc_copts(),
    deps = [
        ":cc",
        "@com_google_googletest//:gtest_main",
    ],
)
